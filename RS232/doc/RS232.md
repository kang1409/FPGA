# 串口(UART)RS232

串口作为常用的三大低速总线（UART,   SPI,    IIC）之一；与SPI , IIC 不同的是，UART 是异步通信接口；

## 通信协议

rs232没有时钟线，只有俩个位宽为1bit的数据线rx（用于接受数据的线）,tx（用于发送数据的线），数据的发送是基于帧结构，即一帧一帧的发送数据与接受数据；每一帧的结构为    起始位0，8比特的数据，停止位1，空闲状态为高电平；

这里使用9600的波特率；进行解释

码元：在信息传递传输通道中携带数据信息的信号单元，（串口是1bit进行传输的，码元就是一个二进制数）

波特率：每秒中通过信号传输的码元数；（选用9600Baud）

比特率：每秒钟通信信道传输的信息量为位传输量，简称比特率；(9600Bps = 9600Baud * 1bit)

计数个数（cnt = (1s * 10^9)ns / 9600bit ）* (周期为10)  = 10416个周期；

## 实战目标

实现串口协议rs232 

## 内容

### 模块说明

| 模块名称 | 功能                                             |
| -------- | ------------------------------------------------ |
| rs232    | rs232回环测试顶层模块                            |
| uart_rx  | rs232串口数据接受模块          1bit------->8bit  |
| uart_tx  | rs232串口数据发送模块           8bit------->1bit |

### rs232

#### 信号描述

| 信号 | 位宽 | 类型   | 功能     |
| ---- | ---- | ------ | -------- |
| rst  | 1'b  | input  | 复位     |
| clk  | 1'b  | input  | 时钟     |
| rx   | 1'b  | input  | 接收信号 |
| tx   | 1'b  | output | 输出信号 |

#### 代码

```verilog

module rs232
(
    input wire clk,
    input wire rst,
    input wire rx,

    output wire tx
    );

    wire [7:0] data;
    wire       flag;

    parameter  UART_BPS = 14'd9600;
    parameter  CLK      = 27'd100_000_000;

    uart_rx     
    #(
        .UART_BPS (UART_BPS),
        .CLK      (CLK)
    )
    uart_rx_inst
    (

        .clk(clk),
        .rst(rst),
        .rx(rx),

        .po_data(data),
        .po_data_flag(flag)
    );

    uart_tx 
    #(
        .UART_BPS (UART_BPS),
        .CLK      (CLK)
    ) 
    uart_tx_inst
   (
        .clk(clk),
        .rst(rst),
        .pi_data(data),
        .pi_data_flag(flag),

        .tx(tx)
    );
    
endmodule

```

### uart_rx

#### 信号描述

| 信号         | 位宽 | 类型   | 功能         |
| ------------ | ---- | ------ | ------------ |
| rst          | 1'b  | input  |              |
| clk          | 1'b  | input  |              |
| rx           | 1'b  | input  |              |
| po_data      | 8'b  | output | 获取的数据   |
| po_data_flag | 1'b  | output | 数据有效标志 |

#### 代码

```verilog

module uart_rx
#(
    parameter  UART_BPS = 14'd9600,
    parameter  CLK      = 27'd100_000_000
)(
    input wire clk,
    input wire rst,
    input wire rx,

    output reg [7:0] po_data,
    output reg       po_data_flag
    );
    localparam BAUD_CNT_MAX = CLK / UART_BPS;
    localparam CNT_BIT_MAX = 4'd9;

    reg rx_reg1;
    reg rx_reg2;
    reg rx_reg3;

    reg stat_work;
    reg work_en;

    reg [13:0]  cnt;
    reg [3:0]   cnt_bit;

    reg         bit_flag;
    reg [7:0]   rx_data;
    reg         rx_flag;
    //通过二级寄存器消除亚稳态
    always @(negedge clk or posedge rst) begin
        if(rst == 1'b1)
            rx_reg1 <= 1'b1;
        else
            rx_reg1 <= rx;
    end

    always @(negedge clk or posedge rst) begin
        if(rst == 1'b1)
            rx_reg2 <= 1'b1;
        else
            rx_reg2 <= rx_reg1;
    end

    always @(negedge clk or posedge rst) begin
        if(rst == 1'b1)
            rx_reg3 <= 1'b1;
        else
            rx_reg3 <= rx_reg2;
    end

    //reg2 与  reg3  下降沿判断
    always @(negedge clk or posedge rst) begin
        if(rst == 1'b1)
            stat_work <= 1'b0;
        else if(((~rx_reg2) && (rx_reg3)) == 1'b1)
            stat_work <= 1'b1;
        else 
            stat_work <= 1'b0;
    end

    //就收数据使能信号   work_en
    always @(negedge clk or posedge rst) begin
        if(rst == 1'b1)
            work_en <= 1'b0;
        else if(stat_work == 1'b1) 
            work_en <= 1'b1;
        else if((bit_flag == 1'b1) && (cnt_bit == 4'd8))
            work_en <= 1'b0;
        else 
            work_en <= work_en;

    end

    //计数器 0~BAUD_CNT_MAX
    always @(negedge clk or posedge rst) begin
        if(rst == 1'b1)
            cnt <= 1'b0;
        else if(cnt == BAUD_CNT_MAX)
            cnt <= 1'b0;
        else if(work_en == 1'b1)
            cnt <= cnt + 1'b1;
        else 
            cnt <= 1'b0;
    end
    //计数器比特数
    always @(negedge clk or posedge rst ) begin
        if(rst == 1'b1)
            cnt_bit <= 1'b0;
        else if((cnt_bit == 4'd8) && (bit_flag == 1'b1))
            cnt_bit <= 1'b0;
        else if(cnt == BAUD_CNT_MAX - 1'b1)
            cnt_bit <= cnt_bit + 1'b1;
        else 
            cnt_bit <= cnt_bit;
    end

    // 计数器中间时数据最稳定,标志信号 可对除法进行优化
    always @( negedge clk or posedge rst ) begin
        if(rst == 1'b1)
            bit_flag <= 1'b0;
        else if(cnt == (BAUD_CNT_MAX / 2) - 1)
            bit_flag <= 1'b1;
        else 
            bit_flag <= 1'b0;
    end

    //拿取数据进行移位拼凑;
    always @(negedge clk or posedge rst ) begin
        if(rst == 1'b1)
            rx_data <= 8'b0;
        else if((bit_flag == 1'b1) && (cnt_bit >= 4'd1) && (cnt_bit <= 4'd8)) 
            rx_data <= {rx_reg3, rx_data[7:1]};
        else
            rx_data <= rx_data;
    end

    // 取到完整数据之后。生成标志信号
    always @(negedge clk or posedge rst ) begin
        if(rst == 1'b1)
            rx_flag <= 1'b0;
        else if((bit_flag == 1'b1) && (cnt_bit == 4'd8))
            rx_flag <= 1'b1;
        else 
            rx_flag <= 1'b0;
    end

    //输出数据与标志信号
    always @(negedge clk or posedge rst ) begin
        if(rst == 1'b1)
            po_data <= 8'd0;
        else if(rx_flag == 1'b1)
            po_data <= rx_data;
        else 
            po_data <= po_data;
    end
    always @(negedge clk or posedge rst) begin
        if(rst == 1'b1)
            po_data_flag <= 1'b0;
        else 
            po_data_flag <= rx_flag;
    end
endmodule

```



### uart_tx

#### 信号描述

| 信号         | 位宽 | 类型   | 功能     |
| ------------ | ---- | ------ | -------- |
| clk          | 1'b  | input  |          |
| rst          | 1'b  | input  |          |
| pi_data      | 8'b  | input  |          |
| pi_data_flag | 1'b  | input  |          |
| tx           | 1'b  | output | 输出信号 |

#### 代码

```verilog

module uart_tx
#(
    parameter  UART_BPS = 14'd9600,
    parameter  CLK      = 27'd100_000_000
)(
    input wire          clk,
    input wire          rst,
    input wire [7:0]    pi_data,
    input wire          pi_data_flag,

    output reg          tx
    );
    localparam  CNT_MAX = CLK / UART_BPS;

    reg [13:0]      cnt;
    reg [3:0]       bit_cnt;


    reg             work_en;

    //工作使能
    always @(negedge clk or posedge rst ) begin
        if(rst == 1'b1)
            work_en <= 1'b0;
        else if(pi_data_flag == 1'b1)
            work_en <= 1'b1;
        else if((bit_cnt == 4'd9) && (cnt == CNT_MAX - 2'd2))
            work_en <= 1'b0;
        else 
            work_en <= work_en;
    end

    //计数器
    always @(negedge clk or posedge rst ) begin
        if(rst == 1'b1)
            cnt <= 1'b0;
        else if(cnt == CNT_MAX - 1'b1)
            cnt <= 1'b0;
        else if(work_en == 1'b1)
            cnt <= cnt + 1'b1;
        else
            cnt <= 1'b0;
    end

    //bit_cnt 计数器
    always @(negedge clk or posedge rst ) begin
        if (rst == 1'b1)
            bit_cnt <= 1'b0;
        else if(cnt == CNT_MAX - 1'b1)
            bit_cnt = bit_cnt + 1'b1;
        else if(work_en == 1'b0)
            bit_cnt <= 1'b0;
    end

    always @(posedge rst or negedge clk) begin
        if(rst == 1'b1)
            tx <= 1'b1;
        else if(work_en == 1'b1)
            case (bit_cnt)
                0:tx <= 1'b0;
                1:tx <= pi_data[0] ;
                2:tx <= pi_data[1] ;
                3:tx <= pi_data[2] ;
                4:tx <= pi_data[3] ;
                5:tx <= pi_data[4] ;
                6:tx <= pi_data[5] ;
                7:tx <= pi_data[6] ;
                8:tx <= pi_data[7] ;
                9:tx <= 1'b1 ;
                default: tx <= 1'b1;
            endcase
        else 
            tx <= 1'b1;
    end
    
endmodule

```

## 总结

task       endtask     可以互相调用

for 在测试文件中很高效；